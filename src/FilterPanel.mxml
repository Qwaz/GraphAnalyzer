<?xml version="1.0" encoding="utf-8"?>
<s:Panel xmlns:fx="http://ns.adobe.com/mxml/2009" 
		 xmlns:s="library://ns.adobe.com/flex/spark" 
		 xmlns:mx="library://ns.adobe.com/flex/mx" xmlns:local="*"
		 creationComplete="init()">
	<fx:Script>
		<![CDATA[
			import events.ListClickEvent;
			
			import flash.events.MouseEvent;
			import filters.*;
			
			import mx.collections.ArrayList;
			import utils.getClassReference;
			
			public static var panel:FilterPanel;
			
			[Bindable]
			private static var filterList:ArrayList = new ArrayList([
				C_LTFilter,
				C_NotFilter,
				C_OrFilter,
				C_AndFilter,
				T_AlwaysFilter,
				T_OnceFilter,
				A_InvisibleFilter,
				A_VisibleFilter,
				A_WeightFilter,
				A_LinearWeightFilter,
				A_LogarithmicWeightFilter,
			]);
			
			[Bindable]
			private static var _appliedList:ArrayList = new ArrayList();
			
			private static var LastAppliedList:ArrayList;
			private static var ncon:int = 1, ntime:int = 1, napply:int = 1;
			
			public static function get appliedList():ArrayList {
				return _appliedList;
			}
			
			private function init():void {
				panel = this;
				ApplyButtonPressed();
				appliedFilter.addEventListener(MouseEvent.MOUSE_WHEEL, slowWheel, true);
			}
			
			private function addFilter(e:ListClickEvent):void {
				var selectedFilter:Class = filterList.getItemAt(e.index) as Class;
				var newFilter:Filter = new selectedFilter();
				
				if (newFilter is ConditionFilter)
				{
					newFilter.filterName = "C" + (ncon++).toString();
				}
				else if (newFilter is TimeFilter)
				{
					newFilter.filterName = "T" + (ntime++).toString();
				}
				else if (newFilter is ApplyFilter)
				{
					newFilter.filterName = "A" + (napply++).toString();
				}
				else
				{
					throw new Error("새로운 필터????");
				}
				
				_appliedList.addItem(newFilter);
			}
			
			private function slowWheel(e:MouseEvent):void {
				e.delta = e.delta > 0 ? 1 : -1;
			}
			
			private function reset():void
			{
				for (var i:int = 0; i < filterList.length; i++) {
					var filter:Class = filterList.getItemAt(i) as Class;
					var kakaotalk:Object = new filter;
					
					if (kakaotalk is ApplyFilter)
					{
						(kakaotalk as ApplyFilter).reset();
					}
				}
			}
			
			public function ApplyButtonPressed():void
			{
				LastAppliedList = new ArrayList();
				for (var i:int = 0; i < _appliedList.length; ++i)
				{
					var selectedFilter:Class;
					var nowFilter:Filter;
					var len:int;
					
					selectedFilter = getClassReference(_appliedList.getItemAt(i));
					nowFilter = new selectedFilter();
					if (nowFilter is ApplyFilter) {
						ApplyFilter(nowFilter).activated = ApplyFilter(_appliedList.getItemAt(i)).activated;
					}
					LastAppliedList.addItem(nowFilter);
					nowFilter.filterName = (_appliedList.getItemAt(i) as Filter).filterName;
					len = nowFilter.getParameters().length;
					
					nowFilter.params.removeAll();
					for (var j:int = 0; j < len; ++j)
					{
						nowFilter.params.addItem(
							(_appliedList.getItemAt(i) as Filter).params.getItemAt(j));
					}
					
					nowFilter.initParameter(LastAppliedList);
				}
				
				render();
			}
			
			public function render():void {
				var i:int, j:int, end:int;
				var fail:Boolean;
				
				reset();
				
				fail = false;
				for (i = 0; i < LastAppliedList.length; i++) {
					var now:Filter = LastAppliedList.getItemAt(i) as Filter;
					
					for (j = 0; j < now.getParameters().length; j++) {
						if (now.parameter[j] == null) {
							result.text = (i + 1) + "번째 필터의 " + (j + 1) + "번째 매개변수가 잘못되었습니다.";
							fail = true;
							break;
						}
					}
					if (fail) break;
					
					if (now is ApplyFilter && (now as ApplyFilter).activated)
					{
						try
						{
							(now as ApplyFilter).apply();
						}
						catch (e:Error)
						{
							result.text = e.message;
							fail = true;
							break;
						}
					}
				}
				
				if (!fail)
				{
					Canvas.canvas.reDraw();
					result.text = "성공!";
				}
			}
		]]>
	</fx:Script>
	
	<mx:VDividedBox width="100%" height="100%" liveDragging="true">
		<s:VGroup width="100%" height="70%">
			<s:List id="appliedFilter" verticalScrollPolicy="on" horizontalScrollPolicy="off"
			width="100%" height="100%" dataProvider="{_appliedList}" itemRenderer="renderers.AppliedFilterRenderer"/>

			<s:HGroup width="100%" height="20">
				<s:Label id="result" width="100%">초기화</s:Label>
				<s:Button click="ApplyButtonPressed()">적용</s:Button>
			</s:HGroup>
		</s:VGroup>
		
		<s:List id="filterSelector" width="100%" height="30%" dataProvider="{filterList}" itemRenderer="renderers.FilterSelectorRenderer"
		creationComplete="filterSelector.addEventListener('listClickEvent', addFilter);">
		</s:List>
	</mx:VDividedBox>
</s:Panel>